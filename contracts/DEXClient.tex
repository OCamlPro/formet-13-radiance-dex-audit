
\chapter{Contract DEXClient}

\minitoc

In file {\tt DEXClient.sol}

\section{Contract Inheritance}


\noindent\begin{tabular}{|l|p{5cm}|}\hline
ITokensReceivedCallback & \\\hline
IDEXClient & \\\hline
IDEXConnect & \\\hline
\end{tabular}


\section{Type Definitions}

\subsection{Struct Connector}

\begin{itemize}
  \item OK
%\item Minor Issue: this method should check {\tt require(statusConnected,..)}
%\item \issueError{}
%\item \issueEncodeBody{}
\end{itemize}

\begin{lstlisting}[firstnumber=29]
  struct Connector {
    address root_address;
    uint256 souint;
    bool status;
  }
\end{lstlisting}

\subsection{Struct Callback}


\begin{itemize}
\item Minor Issue (readability): {\tt payload\_arg0}, {\tt payload\_arg1},
  {\tt payload\_arg2} should be renamed to more explicit names
\end{itemize}

\begin{lstlisting}[firstnumber=43]
  struct Callback {
    address token_wallet;
    address token_root;
    uint128 amount;
    uint256 sender_public_key;
    address sender_address;
    address sender_wallet;
    address original_gas_to;
    uint128 updated_balance;
    uint8 payload_arg0;
    address payload_arg1;
    address payload_arg2;
  }
\end{lstlisting}

\subsection{Struct Pair}

\begin{itemize}
  \item OK
\end{itemize}

\begin{lstlisting}[firstnumber=60]
  struct Pair {
    bool status;
    address rootA;
    address walletA;
    address rootB;
    address walletB;
    address rootAB;
  }
\end{lstlisting}

\section{Constant Definitions}

\begin{itemize}
  \item \issueUnits{}
\end{itemize}

\begin{lstlisting}[firstnumber=23]
  uint128 constant GRAMS_CONNECT_PAIR = 500000000;
\end{lstlisting}

\begin{lstlisting}[firstnumber=24]
  uint128 constant GRAMS_SET_CALLBACK_ADDR = 100000000;
\end{lstlisting}

\begin{lstlisting}[firstnumber=25]
  uint128 constant GRAMS_SWAP = 500000000;
\end{lstlisting}

\begin{lstlisting}[firstnumber=26]
  uint128 constant GRAMS_PROCESS_LIQUIDITY = 500000000;
\end{lstlisting}

\begin{lstlisting}[firstnumber=27]
  uint128 constant GRAMS_RETURN_LIQUIDITY = 500000000;
\end{lstlisting}

\section{Static Variable Definitions}

\begin{itemize}
\item \issueStatic{}
\item Minor Issue: Deployment messages are limited to 16 kB, and
  contain the code of the contract, the static variables and the
  constructor arguments. As {\tt codeDEXConnector} is a static
  variable, the deployment message will contain the code of DEXClient
  and DEXConnector at the same time. It could become an issue in the
  future if their codes increase in size. If it is important to use
  DEXConnector code static to distinguish clients, it might be worth
  replacing it by a hash and use another message to initialize the
  variable instead.
\end{itemize}

\begin{lstlisting}[firstnumber=18]
  address static public rootDEX;
\end{lstlisting}

\begin{lstlisting}[firstnumber=19]
  uint256 static public soUINT;
\end{lstlisting}

\begin{lstlisting}[firstnumber=20]
  TvmCell static public codeDEXConnector;
\end{lstlisting}

\section{Variable Definitions}

\begin{itemize}
\item \issueGlobal{}
\end{itemize}

\begin{lstlisting}[firstnumber=35]
  address[] public rootKeys;
\end{lstlisting}

\begin{lstlisting}[firstnumber=36]
  mapping (address => address) public rootWallet;
\end{lstlisting}

\begin{lstlisting}[firstnumber=37]
  mapping (address => address) public rootConnector;
\end{lstlisting}

\begin{lstlisting}[firstnumber=38]
  mapping (address => Connector) connectors;
\end{lstlisting}

\begin{lstlisting}[firstnumber=40]
  uint public counterCallback;
\end{lstlisting}

\begin{lstlisting}[firstnumber=57]
  mapping (uint => Callback) callbacks;
\end{lstlisting}

\begin{lstlisting}[firstnumber=69]
  mapping(address => Pair) public pairs;
\end{lstlisting}

\begin{lstlisting}[firstnumber=70]
  address[] public pairKeys;
\end{lstlisting}

\section{Modifier Definitions}

\subsection{Modifier alwaysAccept}

\begin{itemize}
\item \issueAlwaysAccept{DEXClient}
\end{itemize}

\begin{lstlisting}[firstnumber=73]
  modifier alwaysAccept {
    tvm.accept();
    _;
  }
\end{lstlisting}

\subsection{Modifier checkOwnerAndAccept}

\begin{itemize}
\item \issueError{}
\end{itemize}

\begin{lstlisting}[firstnumber=79]
  modifier checkOwnerAndAccept {
    require(msg.pubkey() == tvm.pubkey(), 102);
    tvm.accept();
    _;
  }
\end{lstlisting}

\section{Constructor Definitions}

\subsection{Constructor}

\begin{itemize}
\item \issueError{}
\item Minor Issue (Semantics): {\tt counterCallback} should probably be
  initialized to 1 instead of 0, and keep 0 as the specific value of
  {\tt getFirstCallback} when no callback is available.
\end{itemize}

\begin{lstlisting}[firstnumber=85]
  constructor() public {
    require(msg.sender == rootDEX, 103);
    tvm.accept();
    counterCallback = 0;
  }
\end{lstlisting}

\section{Public Method Definitions}

\subsection{Receive function}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=413]
  receive() external {
  }
\end{lstlisting}

\subsection{Function connectCallback}

\begin{itemize}
\item \issueEncodeBody
\item \issueAccept{DEXClient.connectCallback}{The balance of the
  contract could be drained, by sending many {\tt connectCallback}
  messages. Replace the {\tt if} by a {\tt require()} and perform the
  {\tt tvm.accept} only afterwards.}
\end{itemize}

\begin{lstlisting}[firstnumber=181]
  function connectCallback(address wallet) public override alwaysAccept {
    address connector = msg.sender;
    if (connectors.exists(connector)) {
      Connector cc = connectors[connector];
      rootKeys.push(cc.root_address);
      rootWallet[cc.root_address] = wallet;
      rootConnector[cc.root_address] = connector;
      TvmCell bodySTC = tvm.encodeBody(IDEXConnector(connector).setTransferCallback);
      connector.transfer({value: GRAMS_SET_CALLBACK_ADDR, bounce:true, flag: 0, body:bodySTC});
      TvmCell bodySBC = tvm.encodeBody(IDEXConnector(connector).setBouncedCallback);
      connector.transfer({value: GRAMS_SET_CALLBACK_ADDR, bounce:true, flag: 0, body:bodySBC});
      cc.status = true;
      connectors[connector] = cc;
    }
  }
\end{lstlisting}

\subsection{Function connectPair}

\begin{itemize}
\item \issueEncodeBody
\end{itemize}

\begin{lstlisting}[firstnumber=92]
  function connectPair(address pairAddr) public checkOwnerAndAccept  returns (bool statusConnection) {
    statusConnection = false;
    if (!pairs.exists(pairAddr)){
      Pair cp = pairs[pairAddr];
      cp.status = false;
      pairs[pairAddr] = cp;
      pairKeys.push(pairAddr);
      TvmCell body = tvm.encodeBody(IDEXPair(pairAddr).connect);
      pairAddr.transfer({value:GRAMS_CONNECT_PAIR, body:body});
      statusConnection = true;
    }
  }
\end{lstlisting}

\subsection{Function connectRoot}

\begin{itemize}
\item \issuePartial{}. The method should check for a minimal balance
  in a {\tt require()} before {\tt tvm.accept}
\item \issueEncodeBody{}
\end{itemize}

\begin{lstlisting}[firstnumber=158]
  function connectRoot(address root, uint256 souint, uint128 gramsToConnector, uint128 gramsToRoot) public checkOwnerAndAccept returns (bool statusConnected){
    statusConnected = false;
    if (!rootWallet.exists(root)) {
      TvmCell stateInit = tvm.buildStateInit({
        contr: DEXConnector,
        varInit: { soUINT: souint, dexclient: address(this) },
        code: codeDEXConnector,
        pubkey: tvm.pubkey()
      });
      TvmCell init = tvm.encodeBody(DEXConnector);
      address connector = tvm.deploy(stateInit, init, gramsToConnector, address(this).wid);
      Connector cr = connectors[connector];
      cr.root_address = root;
      cr.souint = souint;
      cr.status = false;
      connectors[connector] = cr;
      TvmCell body = tvm.encodeBody(IDEXConnector(connector).deployEmptyWallet, root);
      connector.transfer({value:gramsToRoot, bounce:true, body:body});
      statusConnected = true;
    }
  }
\end{lstlisting}

\subsection{Function createNewPair}

\begin{itemize}
\item Minor Issue: a {\tt require} is executed after {\tt tvm.accept},
  which may cause replication of failure and heavy gas cost. In
  general, we recommend to remove {\tt tvm.accept} from modifiers, so
  that it can be explicitely performed after all {\tt requires}.
\item \issueEncodeBody{}
\item \issueError{}
\end{itemize}

\begin{lstlisting}[firstnumber=356]
  function createNewPair(
    address root0,
		address root1,
		uint256 pairSoArg,
		uint256 connectorSoArg0,
		uint256 connectorSoArg1,
		uint256 rootSoArg,
		bytes rootName,
		bytes rootSymbol,
		uint8 rootDecimals,
		uint128 grammsForPair,
		uint128 grammsForRoot,
		uint128 grammsForConnector,
		uint128 grammsForWallet,
    uint128 grammsTotal
  ) public view checkOwnerAndAccept  {
    require (!(grammsTotal < (grammsForPair+2*grammsForConnector+2*grammsForWallet+grammsForRoot)) && !(grammsTotal < 5 ton),106);
    require (!(address(this).balance < grammsTotal),105);
    TvmCell body = tvm.encodeBody(IDEXRoot(rootDEX).createDEXpair, root0,root1,pairSoArg,connectorSoArg0,connectorSoArg1,rootSoArg,rootName,rootSymbol,rootDecimals,grammsForPair,grammsForRoot,grammsForConnector,grammsForWallet);
    rootDEX.transfer({value:grammsTotal, bounce:false, flag: 1, body:body});
  }
\end{lstlisting}

\subsection{Function getAllDataPreparation}

\begin{itemize}
\item \issueAccept{DEXClient.getAllDataPreparation}{The balance of the
  contract could be drained, by sending many {\tt
    getAllDataPreparation} messages, especially as the return values
  may be expensive to serialize. Accept only for owner or use it as a
  get-method (executed locally, without gas).}
\end{itemize}

\begin{lstlisting}[firstnumber=215]
  function getAllDataPreparation() public view alwaysAccept returns(address[] pairKeysR, address[] rootKeysR){
    pairKeysR = pairKeys;
    rootKeysR = rootKeys;
  }
\end{lstlisting}

\subsection{Function getBalance}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=351]
  function getBalance() public pure responsible returns (uint128) {
    return { value: 0, bounce: false, flag: 64 } thisBalance();
  }
\end{lstlisting}

\subsection{Function getCallback}

\begin{itemize}
\item Minor Issue (Gas loss): there is probably no need to spend gas
  with {\tt tvm.accept} since the method can be executed locally
  (get-method).
\item Minor Issue (Semantics): the method should probably fail with
  {\tt require} if the callback id does not exist.
\end{itemize}

\begin{lstlisting}[firstnumber=318]
  function getCallback(uint id) public view checkOwnerAndAccept returns (
    address token_wallet,
    address token_root,
    uint128 amount,
    uint256 sender_public_key,
    address sender_address,
    address sender_wallet,
    address original_gas_to,
    uint128 updated_balance,
    uint8 payload_arg0,
    address payload_arg1,
    address payload_arg2
  ){
    Callback cc = callbacks[id];
    token_wallet = cc.token_wallet;
    token_root = cc.token_root;
    amount = cc.amount;
    sender_public_key = cc.sender_public_key;
    sender_address = cc.sender_address;
    sender_wallet = cc.sender_wallet;
    original_gas_to = cc.original_gas_to;
    updated_balance = cc.updated_balance;
    payload_arg0 = cc.payload_arg0;
    payload_arg1 = cc.payload_arg1;
    payload_arg2 = cc.payload_arg2;
  }
\end{lstlisting}

\subsection{Function getConnectorAddress}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=153]
  function getConnectorAddress(uint256 connectorSoArg) public view responsible returns (address) {
    return { value: 0, bounce: false, flag: 64 } computeConnectorAddress( connectorSoArg);
  }
\end{lstlisting}

\subsection{Function getPairData}

\begin{itemize}
\item \issueAccept{DEXClient.getPairData}{The balance of the contract
  could be drained, by sending many {\tt getPairData} messages. Accept
  only for owner or use it as a get-method (executed locally, without
  gas).
\item Minor Issue: the method should probably fail if the associated
  pair does not exist in {\tt pairs}}
\end{itemize}

\begin{lstlisting}[firstnumber=379]
  function getPairData(address pairAddr) public view alwaysAccept returns (
    bool pairStatus,
    address pairRootA,
    address pairWalletA,
    address pairRootB,
    address pairWalletB,
    address pairRootAB,
    address curPair
  ){
    Pair cp = pairs[pairAddr];
    pairStatus = cp.status;
    pairRootA = cp.rootA;
    pairWalletA = cp.walletA;
    pairRootB = cp.rootB;
    pairWalletB = cp.walletB;
    pairRootAB = cp.rootAB;
    curPair = pairAddr;
  }
\end{lstlisting}

\subsection{Function processLiquidity}

\begin{itemize}
\item \issueEncodeBody
\item \issueNumber{}. The payload opcode should be a constant.
\item \issuePartial{}. The method should check the balance with {\tt
  require} before performing {\tt tvm.accept}
\item Minor Issue: Repeated Code. The code could be simplified by
  using an internal function to perform the same computation for {\tt
    rootA} and {\tt rootB}.
\end{itemize}

\begin{lstlisting}[firstnumber=251]
  function processLiquidity(address pairAddr, uint128 qtyA, uint128 qtyB) public view checkOwnerAndAccept returns (bool processLiquidityStatus) {
    processLiquidityStatus = false;
    if (isReadyToProvide(pairAddr)) {
      Pair cp = pairs[pairAddr];
      address connectorA = rootConnector[cp.rootA];
      address connectorB = rootConnector[cp.rootB];
      TvmBuilder builderA;
      builderA.store(uint8(2), address(this), rootWallet[cp.rootAB]);
      TvmCell payloadA = builderA.toCell();
      TvmBuilder builderB;
      builderB.store(uint8(2), address(this), rootWallet[cp.rootAB]);
      TvmCell payloadB = builderB.toCell();
      TvmCell bodyA = tvm.encodeBody(IDEXConnector(connectorA).transfer, cp.walletA, qtyA, payloadA);
      TvmCell bodyB = tvm.encodeBody(IDEXConnector(connectorB).transfer, cp.walletB, qtyB, payloadB);
      connectorA.transfer({value: GRAMS_PROCESS_LIQUIDITY, bounce:true, body:bodyA});
      connectorB.transfer({value: GRAMS_PROCESS_LIQUIDITY, bounce:true, body:bodyB});
      processLiquidityStatus = true;
    }
  }
\end{lstlisting}

\subsection{Function processSwapA}

\begin{itemize}
\item \issueEncodeBody{}
\item \issueNumber{}. The payload opcode should be a constant.
\item \issuePartial{}. The method should check the balance with {\tt
  require} before performing {\tt tvm.accept}
\item Minor Issue: Repeated Code. Methods {\tt processSwapA} and {\tt
  processSwapB} could be simplified by using an internal function for
  shared code.
\end{itemize}

\begin{lstlisting}[firstnumber=221]
  function processSwapA(address pairAddr, uint128 qtyA) public view checkOwnerAndAccept returns (bool processSwapStatus) {
    processSwapStatus = false;
    if (isReady(pairAddr)) {
      Pair cp = pairs[pairAddr];
      address connector = rootConnector[cp.rootA];
      TvmBuilder builder;
      builder.store(uint8(1), cp.rootB, rootWallet[cp.rootB]);
      TvmCell payload = builder.toCell();
      TvmCell body = tvm.encodeBody(IDEXConnector(connector).transfer, cp.walletA, qtyA, payload);
      connector.transfer({value: GRAMS_SWAP, bounce:true, body:body});
      processSwapStatus = true;
    }
  }
\end{lstlisting}

\subsection{Function processSwapB}

\begin{itemize}
\item \issueNumber{}. The payload opcode should be a constant.
\item \issueEncodeBody
\item \issuePartial{}. The method should check the balance with {\tt
  require} before performing {\tt tvm.accept}
\end{itemize}

\begin{lstlisting}[firstnumber=236]
  function processSwapB(address pairAddr, uint128 qtyB) public view checkOwnerAndAccept returns (bool processSwapStatus) {
    processSwapStatus = false;
    if (isReady(pairAddr)) {
      Pair cp = pairs[pairAddr];
      address connector = rootConnector[cp.rootB];
      TvmBuilder builder;
      builder.store(uint8(1), cp.rootA, rootWallet[cp.rootA]);
      TvmCell payload = builder.toCell();
      TvmCell body = tvm.encodeBody(IDEXConnector(connector).transfer, cp.walletB, qtyB, payload);
      connector.transfer({value: GRAMS_SWAP, bounce:true, body:body});
      processSwapStatus = true;
    }
  }
\end{lstlisting}

\subsection{Function returnLiquidity}

\begin{itemize}
\item \issueEncodeBody
\item \issueNumber{}. The payload opcode should be a constant.
\item \issuePartial{}. The method should check the balance with {\tt
  require} before performing {\tt tvm.accept}
\end{itemize}

\begin{lstlisting}[firstnumber=272]
  function returnLiquidity(address pairAddr, uint128 tokens) public view checkOwnerAndAccept returns (bool returnLiquidityStatus) {
    returnLiquidityStatus = false;
    if (isReadyToProvide(pairAddr)) {
    Pair cp = pairs[pairAddr];
    TvmBuilder builder;
    builder.store(uint8(3), rootWallet[cp.rootA], rootWallet[cp.rootB]);
    TvmCell callback_payload = builder.toCell();
    TvmCell body = tvm.encodeBody(IDEXConnector(rootConnector[cp.rootAB]).burn, tokens, pairAddr, callback_payload);
    rootConnector[cp.rootAB].transfer({value:GRAMS_RETURN_LIQUIDITY, body:body});
    returnLiquidityStatus = true;
    }
  }
\end{lstlisting}

\subsection{Function sendTokens}

\begin{itemize}
\item Minor Issue: the method should use {\tt require} instead of {\tt
  if} (before doing the {\tt tvm.accept}).
\item \issueEncodeBody
\item \issueNumber{}. The payload opcode should be a constant.
\item \issuePartial{}. The method should check the balance with {\tt
  require} before performing {\tt tvm.accept}
\end{itemize}

\begin{lstlisting}[firstnumber=399]
  function sendTokens(address tokenRoot, address to, uint128 tokens, uint128 grams) public checkOwnerAndAccept view returns (bool sendTokenStatus){
    sendTokenStatus = false;
    if (rootConnector[tokenRoot] != address(0)) {
      address connector = rootConnector[tokenRoot];
      TvmBuilder builder;
      builder.store(uint8(4), address(this), rootWallet[tokenRoot]);
      TvmCell payload = builder.toCell();
      TvmCell body = tvm.encodeBody(IDEXConnector(connector).transfer, to, tokens, payload);
      connector.transfer({value: grams, bounce:true, body:body});
      sendTokenStatus = true;
    }
  }
\end{lstlisting}

\subsection{Function setPair}

\begin{itemize}
\item \issueAccept{DEXClient.setPair}{The balance of the
  contract could be drained, by sending many {\tt setPair}
  messages. You should replace the {\tt if} by a {\tt require()}
  followed by {\tt tvm.accept()}.}
\item \issuePartial{}.
\end{itemize}

\begin{lstlisting}[firstnumber=127]
  function setPair(address arg0, address arg1, address arg2, address arg3, address arg4) public alwaysAccept override {
    address dexpair = msg.sender;
    if (pairs.exists(dexpair)){
      Pair cp = pairs[dexpair];
      cp.status = true;
      cp.rootA = arg0;
      cp.walletA = arg1;
      cp.rootB = arg2;
      cp.walletB = arg3;
      cp.rootAB = arg4;
      pairs[dexpair] = cp;
    }
  }
\end{lstlisting}

\subsection{Function tokensReceivedCallback}

\begin{itemize}
\item \issueAccept{DEXClient.tokensReceivedCallback}{
  \begin{itemize}
  \item The balance of the contract could be drained, by sending many
    unexpected {\tt tokensReceivedCallback} messages by an
    attacker. The attack could be improved by sending wrong payloads,
    causing {\tt slice.decode} to fail after {\tt tvm.accept}, causing
    replication of failure on all shards.
  \item An attacker could send many such messages also to include
    incorrect receipts in the {\tt callbacks} mapping, and remove
    correct ones by sending more than 10 such messages. Yet, this part
    of the attack is probably not critical, as receipts are only used
    by humans.
  \item Fix:Accept only when sender is one of the wallets of this
    dexclient or use the {\tt msg.value} gas.
  \end{itemize}}
\end{itemize}

\begin{lstlisting}[firstnumber=286]
  function tokensReceivedCallback(
    address token_wallet,
    address token_root,
    uint128 amount,
    uint256 sender_public_key,
    address sender_address,
    address sender_wallet,
    address original_gas_to,
    uint128 updated_balance,
    TvmCell payload
  ) public override alwaysAccept {
    Callback cc = callbacks[counterCallback];
    cc.token_wallet = token_wallet;
    cc.token_root = token_root;
    cc.amount = amount;
    cc.sender_public_key = sender_public_key;
    cc.sender_address = sender_address;
    cc.sender_wallet = sender_wallet;
    cc.original_gas_to = original_gas_to;
    cc.updated_balance = updated_balance;
    TvmSlice slice = payload.toSlice();
    (uint8 arg0, address arg1, address arg2) = slice.decode(uint8, address, address);
    cc.payload_arg0 = arg0;
    cc.payload_arg1 = arg1;
    cc.payload_arg2 = arg2;
    callbacks[counterCallback] = cc;
    counterCallback++;
    if (counterCallback > 10){delete callbacks[getFirstCallback()];}
  }
\end{lstlisting}

\section{Internal Method Definitions}


\subsection{Function computeConnectorAddress}

\begin{itemize}
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=142]
  function computeConnectorAddress(uint256 souint) private inline view returns (address) {
    TvmCell stateInit = tvm.buildStateInit({
      contr: DEXConnector,
      varInit: { soUINT: souint, dexclient: address(this) },
      code: codeDEXConnector,
      pubkey: tvm.pubkey()
    });
    return address(tvm.hash(stateInit));
  }
\end{lstlisting}

\subsection{Function getFirstCallback}

\begin{itemize}
\item Minor Issue: if no callback is present, the function returns
  0. This value could be reserved for this usage by setting
  {\tt counterCallback} to 1 in the constructor.
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=121]
  function getFirstCallback() private view returns (uint) {
		optional(uint, Callback) rc = callbacks.min();
		if (rc.hasValue()) {(uint number, ) = rc.get();return number;} else {return 0;}
	}
\end{lstlisting}

\subsection{Function getQuotient}

\begin{itemize}
\item Minor Issue (Useless code): This internal function is unused.
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=109]
  function getQuotient(uint128 arg0, uint128 arg1, uint128 arg2) private inline pure returns (uint128) {
    (uint128 quotient, ) = math.muldivmod(arg0, arg1, arg2);
    return quotient;
  }
\end{lstlisting}

\subsection{Function getRemainder}

\begin{itemize}
\item Minor Issue (Useless code): This internal function is unused.
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=115]
  function getRemainder(uint128 arg0, uint128 arg1, uint128 arg2) private inline pure returns (uint128) {
    (, uint128 remainder) = math.muldivmod(arg0, arg1, arg2);
    return remainder;
  }
\end{lstlisting}

\subsection{Function isReady}

\begin{itemize}
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=198]
  function isReady(address pair) private inline view returns (bool) {
    Pair cp = pairs[pair];
    Connector ccA = connectors[rootConnector[cp.rootA]];
    Connector ccB = connectors[rootConnector[cp.rootB]];
    return cp.status && rootWallet.exists(cp.rootA) && rootWallet.exists(cp.rootB) && rootConnector.exists(cp.rootA) && rootConnector.exists(cp.rootB) && ccA.status && ccB.status;
  }
\end{lstlisting}

\subsection{Function isReadyToProvide}

\begin{itemize}
\item \issueInternal
\item Minor Issue (Repeated Code): This function could easily be
  derived from {\tt isReady} with the additional check of {\tt
    rootWallet.exists(cp.rootAB)}
\end{itemize}

\begin{lstlisting}[firstnumber=206]
  function isReadyToProvide(address pair) private inline view returns (bool) {
    Pair cp = pairs[pair];
    Connector ccA = connectors[rootConnector[cp.rootA]];
    Connector ccB = connectors[rootConnector[cp.rootB]];
    return cp.status && rootWallet.exists(cp.rootA) && rootWallet.exists(cp.rootB) && rootWallet.exists(cp.rootAB) && rootConnector.exists(cp.rootA) && rootConnector.exists(cp.rootB) && ccA.status && ccB.status;
  }
\end{lstlisting}

\subsection{Function thisBalance}

\begin{itemize}
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=346]
  function thisBalance() private inline  pure returns (uint128) {
    return address(this).balance;
  }
\end{lstlisting}
