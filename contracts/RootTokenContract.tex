
\chapter{Contract RootTokenContract}

\minitoc

In file {\tt RootTokenContract.sol}

\section{Contract Inheritance}

\noindent\begin{tabular}{|l|p{5cm}|}\hline
IRootTokenContract & \\\hline
IBurnableTokenRootContract & \\\hline
IBurnableByRootTokenRootContract & \\\hline
IPausable & \\\hline
ITransferOwner & \\\hline
ISendSurplusGas & \\\hline
IVersioned & \\\hline
\end{tabular}

\section{Static Variable Definitions}

\begin{itemize}
\item \issueStatic{}
\item Minor issue: deployment messages are limited to 16 kB. Having
  {\tt wallet\_code} a static variable will make the deployment
  message contain the codes of both the {\tt RootTokenContract} and
  {\tt TONTokenWallet}. It might become an issue in later versions.
\end{itemize}

\begin{lstlisting}[firstnumber=28]
    uint256 static _randomNonce;
\end{lstlisting}

\begin{lstlisting}[firstnumber=30]
    bytes public static name;
\end{lstlisting}

\begin{lstlisting}[firstnumber=31]
    bytes public static symbol;
\end{lstlisting}

\begin{lstlisting}[firstnumber=32]
    uint8 public static decimals;
\end{lstlisting}

\begin{lstlisting}[firstnumber=34]
    TvmCell static wallet_code;
\end{lstlisting}

\section{Variable Definitions}

\begin{itemize}
\item \issueGlobal{}
\end{itemize}

\begin{lstlisting}[firstnumber=36]
    uint128 total_supply;
\end{lstlisting}

\begin{lstlisting}[firstnumber=38]
    uint256 root_public_key;
\end{lstlisting}

\begin{lstlisting}[firstnumber=39]
    address root_owner_address;
\end{lstlisting}

\begin{lstlisting}[firstnumber=40]
    uint128 public start_gas_balance;
\end{lstlisting}

\begin{lstlisting}[firstnumber=42]
    bool public paused;
\end{lstlisting}

\section{Modifier Definitions}

\subsection{Modifier onlyOwner}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=458]
    modifier onlyOwner() {
        require(isOwner(), RootTokenContractErrors.error_message_sender_is_not_my_owner);
        _;
    }
\end{lstlisting}

\subsection{Modifier onlyInternalOwner}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=463]
    modifier onlyInternalOwner() {
        require(isInternalOwner(), RootTokenContractErrors.error_message_sender_is_not_my_owner);
        _;
    }
\end{lstlisting}

\section{Constructor Definitions}

\subsection{Constructor}

\begin{itemize}
\item \issueCritical{constructor not strict enough in {\tt
    RootTokenContract}}{The constructor does not check the sender of
  the message. Thus, an attacker that can predict the parameters used
  as static variables by another user or contract can deploy the
  contract and take ownership of it. Fix: {\tt root\_public\_key} and
  {\tt root\_owner\_address} should be static variables.}
\end{itemize}

\begin{lstlisting}[firstnumber=48]
    constructor(uint256 root_public_key_, address root_owner_address_) public {
        require((root_public_key_ != 0 && root_owner_address_.value == 0) ||
                (root_public_key_ == 0 && root_owner_address_.value != 0),
                RootTokenContractErrors.error_define_public_key_or_owner_address);
        tvm.accept();

        root_public_key = root_public_key_;
        root_owner_address = root_owner_address_;

        total_supply = 0;
        paused = false;

        start_gas_balance = address(this).balance;
    }
\end{lstlisting}

\section{Public Method Definitions}


\subsection{Fallback function}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=523]
    fallback() external {
    }
\end{lstlisting}

\subsection{OnBounce function}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=514]
    onBounce(TvmSlice slice) external {
        tvm.accept();
        uint32 functionId = slice.decode(uint32);
        if (functionId == tvm.functionId(ITONTokenWallet.accept)) {
            uint128 latest_bounced_tokens = slice.decode(uint128);
            total_supply -= latest_bounced_tokens;
        }
    }
\end{lstlisting}

\subsection{Function deployEmptyWallet}

\begin{itemize}
\item Minor issue: add {\tt require} to check that {\tt msg.value} is
  enough for the deployment.
\end{itemize}

\begin{lstlisting}[firstnumber=237]
    function deployEmptyWallet(
        uint128 deploy_grams,
        uint256 wallet_public_key_,
        address owner_address_,
        address gas_back_address
    )
        override
        external
    returns (
        address
    ) {
        require((owner_address_.value != 0 && wallet_public_key_ == 0) ||
                (owner_address_.value == 0 && wallet_public_key_ != 0),
                RootTokenContractErrors.error_define_public_key_or_owner_address);

        tvm.rawReserve(address(this).balance - msg.value, 2);

        address wallet = new TONTokenWallet{
            value: deploy_grams,
            flag: 1,
            code: wallet_code,
            pubkey: wallet_public_key_,
            varInit: {
                root_address: address(this),
                code: wallet_code,
                wallet_public_key: wallet_public_key_,
                owner_address: owner_address_
            }
        }();

        if (gas_back_address.value != 0) {
            gas_back_address.transfer({ value: 0, flag: 128 });
        } else {
            msg.sender.transfer({ value: 0, flag: 128 });
        }

        return wallet;
    }
\end{lstlisting}

\subsection{Function deployWallet}

\begin{itemize}
\item \issueMajor{Need gas check in {\tt
    RootTokenContract.deployWallet}}{add {\tt require} to check that
  {\tt msg.value} is enough for the deployment in the case of internal
  owner.}
\end{itemize}

\begin{lstlisting}[firstnumber=164]
    function deployWallet(
        uint128 tokens,
        uint128 deploy_grams,
        uint256 wallet_public_key_,
        address owner_address_,
        address gas_back_address
    )
        override
        external
        onlyOwner
    returns(
        address
    ) {
        require(tokens >= 0);
        require((owner_address_.value != 0 && wallet_public_key_ == 0) ||
                (owner_address_.value == 0 && wallet_public_key_ != 0),
                RootTokenContractErrors.error_define_public_key_or_owner_address);

        if(root_owner_address.value == 0) {
            tvm.accept();
        } else {
            tvm.rawReserve(math.max(start_gas_balance, address(this).balance - msg.value), 2);
        }

        TvmCell stateInit = tvm.buildStateInit({
            contr: TONTokenWallet,
            varInit: {
                root_address: address(this),
                code: wallet_code,
                wallet_public_key: wallet_public_key_,
                owner_address: owner_address_
            },
            pubkey: wallet_public_key_,
            code: wallet_code
        });

        address wallet;

        if(deploy_grams > 0) {
            wallet = new TONTokenWallet{
                stateInit: stateInit,
                value: deploy_grams,
                wid: address(this).wid,
                flag: 1
            }();
        } else {
            wallet = address(tvm.hash(stateInit));
        }

        ITONTokenWallet(wallet).accept(tokens);

        total_supply += tokens;

        if (root_owner_address.value != 0) {
            if (gas_back_address.value != 0) {
                gas_back_address.transfer({ value: 0, flag: 128 });
            } else {
                msg.sender.transfer({ value: 0, flag: 128 });
            }
        }

        return wallet;
    }
\end{lstlisting}

\subsection{Function getDetails}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=77]
    function getDetails() override external view responsible returns (IRootTokenContractDetails) {
        return { value: 0, bounce: false, flag: 64 } IRootTokenContractDetails(
            name,
            symbol,
            decimals,
            root_public_key,
            root_owner_address,
            total_supply
        );
    }
\end{lstlisting}

\subsection{Function getTotalSupply}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=92]
    function getTotalSupply() override external view responsible returns (uint128) {
        return { value: 0, bounce: false, flag: 64 } total_supply;
    }
\end{lstlisting}

\subsection{Function getVersion}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=63]
    function getVersion() override external pure responsible returns (uint32) {
        return 4;
    }
\end{lstlisting}

\subsection{Function getWalletAddress}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=111]
    function getWalletAddress(
        uint256 wallet_public_key_,
        address owner_address_
    )
        override
        external
        view
        responsible
    returns (
        address
    ) {
        require((owner_address_.value != 0 && wallet_public_key_ == 0) ||
                (owner_address_.value == 0 && wallet_public_key_ != 0),
                RootTokenContractErrors.error_define_public_key_or_owner_address);
        return { value: 0, bounce: false, flag: 64 } getExpectedWalletAddress(wallet_public_key_, owner_address_);
    }
\end{lstlisting}

\subsection{Function getWalletCode}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=100]
    function getWalletCode() override external view responsible returns (TvmCell) {
        return { value: 0, bounce: false, flag: 64 } wallet_code;
    }
\end{lstlisting}

\subsection{Function mint}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=282]
    function mint(
        uint128 tokens,
        address to
    )
        override
        external
        onlyOwner
    {
        tvm.accept();

        ITONTokenWallet(to).accept(tokens);

        total_supply += tokens;
    }
\end{lstlisting}

\subsection{Function proxyBurn}

\begin{itemize}
\item Minor issue: add {\tt require} to check that {\tt msg.value} is
  enough to send the message.
\end{itemize}

\begin{lstlisting}[firstnumber=307]
    function proxyBurn(
        uint128 tokens,
        address sender_address,
        address send_gas_to,
        address callback_address,
        TvmCell callback_payload
    )
        override
        external
        onlyInternalOwner
    {
        tvm.rawReserve(address(this).balance - msg.value, 2);

        address send_gas_to_ = send_gas_to;
        address expectedWalletAddress = getExpectedWalletAddress(0, sender_address);

        if (send_gas_to.value == 0) {
            send_gas_to_ = sender_address;
        }

        IBurnableByRootTokenWallet(expectedWalletAddress).burnByRoot{value: 0, flag: 128}(
            tokens,
            send_gas_to_,
            callback_address,
            callback_payload
        );
    }
\end{lstlisting}

\subsection{Function sendExpectedWalletAddress}

\begin{itemize}
\item Minor issue: add {\tt require} to check that {\tt msg.value} is
  enough to send the message.
\end{itemize}

\begin{lstlisting}[firstnumber=134]
    function sendExpectedWalletAddress(
        uint256 wallet_public_key_,
        address owner_address_,
        address to
    )
        override
        external
    {
        tvm.rawReserve(address(this).balance - msg.value, 2);

        address wallet = getExpectedWalletAddress(wallet_public_key_, owner_address_);
        IExpectedWalletAddressCallback(to).expectedWalletAddressCallback{value: 0, flag: 128}(
            wallet,
            wallet_public_key_,
            owner_address_
        );
    }
\end{lstlisting}

\subsection{Function sendPausedCallbackTo}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=423]
    function sendPausedCallbackTo(
        uint64 callback_id,
        address callback_addr
    )
        override
        external
    {
        tvm.rawReserve(address(this).balance - msg.value, 2);
        IPausedCallback(callback_addr).pausedCallback{ value: 0, flag: 128 }(callback_id, paused);
    }
\end{lstlisting}

\subsection{Function sendSurplusGas}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=386]
    function sendSurplusGas(
        address to
    )
        override
        external
        onlyInternalOwner
    {
        tvm.rawReserve(start_gas_balance, 2);
        IReceiveSurplusGas(to).receiveSurplusGas{ value: 0, flag: 128 }();
    }
\end{lstlisting}

\subsection{Function setPaused}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=407]
    function setPaused(
        bool value
    )
        override
        external
        onlyOwner
    {
        tvm.accept();
        paused = value;
    }
\end{lstlisting}

\subsection{Function tokensBurned}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=347]
    function tokensBurned(
        uint128 tokens,
        uint256 sender_public_key,
        address sender_address,
        address send_gas_to,
        address callback_address,
        TvmCell callback_payload
    ) override external {

        require(!paused, RootTokenContractErrors.error_paused);

        address expectedWalletAddress = getExpectedWalletAddress(sender_public_key, sender_address);

        require(msg.sender == expectedWalletAddress, RootTokenContractErrors.error_message_sender_is_not_good_wallet);

        tvm.rawReserve(address(this).balance - msg.value, 2);

        total_supply -= tokens;

        if (callback_address.value == 0) {
            send_gas_to.transfer({ value: 0, flag: 128 });
        } else {
            IBurnTokensCallback(callback_address).burnCallback{value: 0, flag: 128}(
                tokens,
                callback_payload,
                sender_public_key,
                sender_address,
                expectedWalletAddress,
                send_gas_to
            );
        }

    }
\end{lstlisting}

\subsection{Function transferOwner}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=440]
    function transferOwner(
        uint256 root_public_key_,
        address root_owner_address_
    )
        override
        external
        onlyOwner
    {
        require((root_public_key_ != 0 && root_owner_address_.value == 0) ||
                (root_public_key_ == 0 && root_owner_address_.value != 0),
                RootTokenContractErrors.error_define_public_key_or_owner_address);
        tvm.accept();
        root_public_key = root_public_key_;
        root_owner_address = root_owner_address_;
    }
\end{lstlisting}

\section{Internal Method Definitions}


\subsection{Function getExpectedWalletAddress}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=485]
    function getExpectedWalletAddress(
        uint256 wallet_public_key_,
        address owner_address_
    )
        private
        inline
        view
    returns (
        address
    ) {
        TvmCell stateInit = tvm.buildStateInit({
            contr: TONTokenWallet,
            varInit: {
                root_address: address(this),
                code: wallet_code,
                wallet_public_key: wallet_public_key_,
                owner_address: owner_address_
            },
            pubkey: wallet_public_key_,
            code: wallet_code
        });

        return address(tvm.hash(stateInit));
    }
\end{lstlisting}

\subsection{Function isExternalOwner}

\begin{itemize}
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=476]
    function isExternalOwner() private inline view returns (bool) {
        return root_public_key != 0 && root_public_key == msg.pubkey();
    }
\end{lstlisting}

\subsection{Function isInternalOwner}

\begin{itemize}
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=472]
    function isInternalOwner() private inline view returns (bool) {
        return root_owner_address.value != 0 && root_owner_address == msg.sender;
    }
\end{lstlisting}

\subsection{Function isOwner}

\begin{itemize}
\item \issueInternal
\end{itemize}

\begin{lstlisting}[firstnumber=468]
    function isOwner() private inline view returns (bool) {
        return isInternalOwner() || isExternalOwner();
    }
\end{lstlisting}
